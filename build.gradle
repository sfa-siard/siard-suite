plugins {
    id 'java'
    id 'application'
    id 'org.beryx.runtime' version '1.12.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'pl.allegro.tech.build.axion-release' version '1.14.3'
    id 'io.freefair.lombok' version '6.5.0'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

group = 'ch.admin.bar'
description = 'SIARD Suite'

version = scmVersion.version

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8
mainClassName = "ch.admin.bar.siardsuite.Launcher"
applicationName = "SIARD-Suite"

jar {
    manifest {
        attributes(
                'Implementation-Version': '1.0',
                'Main-Class': 'ch.admin.bar.siardsuite.SiardApplication',
                'Class-Path': 'activation-1.1.1.jar       junit-4.12.jar\n' +
                        '            adapter-0.2.0.jar          materialfx-11.13.5.jar\n' +
                        '            antlr-runtime-4.5.2.jar    mslinks.jar\n' +
                        '            commons-lang-2.6.jar       msv-core-2010.2.jar\n' +
                        '            commons-logging-1.1.3.jar  mysql-connector-java-8.0.11.jar\n' +
                        '            db2jcc4.jar                mysql-connector-java-8.0.18.jar\n' +
                        '            enterutils.jar             ojdbc6.jar\n' +
                        '            hamcrest-core-1.3.jar      postgresql-42.2.5.jar\n' +
                        '            ini4j-0.5.4.jar            siardapi.jar\n' +
                        '            jackcess-2.1.6a.jar        siardcmd.jar\n' +
                        '            jaxb-api.jar               siard-suite-2.2.118.jar\n' +
                        '            jaxb-core.jar              sqljdbc41.jar\n' +
                        '            jaxb-impl.jar              sqlparser.jar\n' +
                        '            jdbcaccess.jar             stax2-api-3.1.1.jar\n' +
                        '            jdbcbase.jar               sunjdbc.jar\n' +
                        '            jdbcdb2.jar                virtualizedfx-11.2.6.1.jar\n' +
                        '            jdbcmssql.jar              woodstox-core-lgpl-4.1.2.jar\n' +
                        '            jdbcmysql.jar              woodstox-msv-rng-datatype-20020414.jar\n' +
                        '            jdbcoracle.jar             xdb6.jar\n' +
                        '            jdbcpostgres.jar           \n' +
                        '            jfxrt.jar                  xsdlib-2010.1.jar\n' +
                        '            json-simple-1.1.1.jar      zip64.jar\n' +
                        '            jts-1.14.jar               tika-app-2.8.0.jar'
        )
    }
}

dependencies {

    implementation 'org.glavo.materialfx:materialfx:11.13.5'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.testfx:openjfx-monocle:jdk-12.0.1+2'

    // legacy: add all dependencies from lib folder
    implementation fileTree(include: ['*.jar'], dir: 'lib')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
    jvmArgs("--add-opens=java.xml/com.sun.org.apache.xerces.internal.jaxp=ALL-UNNAMED", "--add-opens=java.xml/com.sun.org.apache.xalan.internal.xsltc.trax=ALL-UNNAMED", "--add-opens=javafx.graphics/com.sun.javafx.application=ALL-UNNAMED", "--add-opens=javafx.graphics/com.sun.glass.ui=ALL-UNNAMED")
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        noConsole = false
        jvmArgs = ["--add-opens=java.xml/com.sun.org.apache.xerces.internal.jaxp=ALL-UNNAMED",
                   "--add-opens=java.xml/com.sun.org.apache.xalan.internal.xsltc.trax=ALL-UNNAMED",
                   "--add-opens=javafx.graphics/com.sun.javafx.application=ALL-UNNAMED",
                   "--add-opens=javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"]
    }

    additive = true
    modules = [
            'java.logging',
            'java.sql',
            'java.management',
            'java.naming'
    ]

    imageZip = file("$buildDir/image-zip/SIARD-Suite.${version}.zip")

    def currentOs = org.gradle.internal.os.OperatingSystem.current()
    if (currentOs.windows) {
        imageZip = file("$buildDir/image-zip/SIARD-Suite.${version}-win.zip")
        imageDir = file("$buildDir/SIARD-Suite.${version}-win")
    } else if (currentOs.linux) {
        imageZip = file("$buildDir/image-zip/SIARD-Suite.${version}-linux.zip")
        imageDir = file("$buildDir/SIARD-Suite.${version}-linux")
    } else if (currentOs.macOsX) {
        imageZip = file("$buildDir/image-zip/SIARD-Suite.${version}-mac.zip")
        imageDir = file("$buildDir/SIARD-Suite.${version}-mac")
    }

    jpackage {
        imageName = "SIARD-Suite"
        installerName = "SIARD-Suite"
        appVersion = version

        skipInstaller = false

        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/ch/admin/bar/siardsuite/icons/archive_red.$imgType"]

        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'BUAR']
        if (currentOs.windows) {
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-menu-group', 'SFA-SIARD', // otherwise, the folder will be named "unknown"
                    '--win-shortcut'
            ]
        } else if (currentOs.linux) {
            installerType = "deb"
            installerOptions += ['--linux-package-name', 'siard-suite', '--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'siard-suite']
        }
    }
}

test {
    useJUnitPlatform()
    // run tets in headless mode -for testFX
    jvmArgs "-Djava.awt.headless=true", "-Dtestfx.headless=true", "--add-opens=java.xml/com.sun.org.apache.xerces.internal.jaxp=ALL-UNNAMED", "--add-opens=java.xml/com.sun.org.apache.xalan.internal.xsltc.trax=ALL-UNNAMED", "--add-opens=javafx.graphics/com.sun.javafx.application=ALL-UNNAMED", "--add-opens=javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"
}


application {
    mainClass = mainClassName
}

processResources {
    filesMatching ('**/version.properties') {
        expand projectVersion: project.version
    }
}

// create additional scripts for siardcmd...
// see https://stackoverflow.com/a/62069262/1310321

task siardFromDb(type: CreateStartScripts) {
    applicationName = "siard-from-db"
    mainClass = 'ch.admin.bar.siard2.cmd.SiardFromDb'
    outputDir = file("build/scripts")
    classpath = project.tasks.getAt(JavaPlugin.JAR_TASK_NAME).outputs.files.plus(project.configurations.getByName(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME))
    // I took this from ApplicationPlugin.java:129
}

task siardToDb(type: CreateStartScripts) {
    applicationName = "siard-to-db"
    mainClass = 'ch.admin.bar.siard2.cmd.SiardToDb'
    outputDir = file("build/scripts")
    classpath = project.tasks.getAt(JavaPlugin.JAR_TASK_NAME).outputs.files.plus(project.configurations.getByName(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME))
    // I took this from ApplicationPlugin.java:129
}

siardFromDb {
    dependsOn(siardToDb)
}

distZip {

    into("${applicationName}-${project.version}/licenses") {
        from 'licenses'
    }
    dependsOn(siardFromDb)
}

asciidoctor {
    languages 'en', 'de', 'fr', 'it'
    baseDirFollowsSourceDir()

    sourceDir 'docs/user-manual/'
    outputDir file('build/docs')
    sources {
        include 'user-manual.adoc'
    }
}


asciidoctorPdf {
    languages 'en', 'de', 'fr', 'it'
    baseDirFollowsSourceDir()

    outputDir 'build/docs/pdf'
    sourceDir 'docs/user-manual/'

    pdfThemes {
        local 'basic', {
            themeDir = 'docs/theme'
            themeName = 'siard-theme'
        }
    }

    asciidoctorj {
        attributes 'media': 'press',
                'styles-dir': 'docs/theme',
                'stylesheet':  'siard-theme.css',
                'source-highlighter': 'coderay',
                'imagesdir': 'images',
                'toc': 'left'
    }
    dependsOn(asciidoctor)
}

tasks.register('copyDocumentation', Copy) {
    from layout.buildDirectory.dir("docs/pdf")
    into layout.projectDirectory.dir('./src/main/resources/ch/admin/bar/siardsuite/doc')
}

tasks.named("asciidoctorPdf") { finalizedBy(copyDocumentation)}
tasks.named("build") { dependsOn(asciidoctorPdf)}
tasks.named("processResources") { dependsOn(copyDocumentation)}
